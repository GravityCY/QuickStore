import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id "fabric-loom" version "1.7-SNAPSHOT"
    id "com.modrinth.minotaur" version "2.+"
    id 'net.darkhax.curseforgegradle' version "1.1.24"
    id "me.gravityio.replacementplugin" version "0.0.7" // my local experimental plugin, just delete if you fork
}

version = project.mod_version
group = "me.gravityio"

def javaVersionNum = project.java_version.toInteger()
def javaVersion = JavaVersion.toVersion(javaVersionNum)
def supportedVersions = project.supported_versions.split(" ").toList()
def supportedEnvironments = project.supported_environments.split(" ").toList()

base {
    archivesName = project.mod_id
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name 'Xander Maven'
        url 'https://maven.isxander.dev/releases'
    }
    maven {
        name = "Terraformers"
        url = "https://maven.terraformersmc.com/"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

project.dependencies.add("minecraft", "com.mojang:minecraft:${project.minecraft_version}")
project.dependencies.add("mappings", "net.fabricmc:yarn:${project.project.yarn_version}:v2")
project.dependencies.add("modImplementation", "net.fabricmc:fabric-loader:${project.loader_version}")
// Optional
project.dependencies.add("modImplementation", "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}")

dependencies {
    modImplementation "dev.isxander:yet-another-config-lib:${yacl_version}-fabric"
    modImplementation "com.terraformersmc:modmenu:${modmenu_version}"
//    modCompileOnly "maven.modrinth:blur-fabric:${blur_version}"
//    modImplementation "maven.modrinth:iris:1.7.0+1.20.6"
}

replacement {
    fileName = "README.md"
    types = ["modrinth", "curseforge"]
    values = [
            "fabric_api_url": ["https://modrinth.com/mod/fabric-api", "https://curseforge.com/minecraft/mc-mods/fabric-api"],
            "yacl_url": ["https://modrinth.com/mod/yacl", "https://curseforge.com/minecraft/mc-mods/yacl"],
            "modmenu_url": ["https://modrinth.com/mod/modmenu", "https://curseforge.com/minecraft/mc-mods/modmenu"]
    ]
}

tasks.register("publishMod") {
    group = "publishing"

    dependsOn "publishCurseForge"
    dependsOn "modrinth"
}

tasks.register('publishCurseForge', TaskPublishCurseForge) {
    disableVersionDetection()

    group = "publishing"
    apiToken = System.getenv("CURSEFORGE_UPLOAD")

    def mainFile = upload(project.curseforge_id, remapJar)
    mainFile.displayName = "${mod_name} ${mod_version}"
    mainFile.changelog = project.changelog
    supportedVersions.each {
        mainFile.addGameVersion(it)
    }
    supportedEnvironments.each {
        mainFile.addEnvironment(it)
    }
    mainFile.addModLoader("Fabric")
    mainFile.releaseType = "release"
    mainFile.addJavaVersion("Java ${javaVersionNum}")

    mainFile.addRequirement("fabric-api")
    mainFile.addRequirement("yacl")
    mainFile.addOptional("modmenu")
}

modrinth {
    token = System.getenv("MODRINTH_UPLOAD")
    projectId = project.modrinth_id
    versionName = "${mod_name} ${mod_version}"
    versionNumber = mod_version
    uploadFile = remapJar
    gameVersions = supportedVersions
    loaders = ["fabric"]
    delegate.changelog = project.changelog
    dependencies {
        required.project "fabric-api"
        required.project "yacl"
        optional.project "modmenu"
    }
}

processResources  {
    def modProperties = [
            "mod_id": project.mod_id,
            "mod_name": project.mod_name,
            "mod_version": project.mod_version,
            "mod_description": project.mod_description,
            "mod_author": project.mod_author,
            "mod_sources": project.mod_sources,
            "mod_license": project.mod_license,
            "loader_version": project.loader_version,
            "minecraft_version": project.minecraft_version,
            "fabric_version": project.fabric_version,
            "java_version": javaVersionNum
    ]

    inputs.properties(modProperties)

    filesMatching("fabric.mod.json") {
        expand modProperties
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = javaVersionNum
}

java {
    withSourcesJar()

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}" }
    }
}